plugins {
    id "io.github.goooler.shadow" version "8.1.7"
    id "java"
    id "net.minecrell.plugin-yml.bukkit" version "0.6.0"
}

group = "net.arcdevs.discordstatusbot"
version = "3.0"
description = "A lightweight minecraft plugin to showcase the status of your server with a single discord message."

repositories {
    mavenCentral() // Apache, BoostedYAML, JDA

    maven{ url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" } // Spigot
    maven{ url "https://jitpack.io" } // Lamp
    maven{ url "https://repo.extendedclip.com/content/repositories/placeholderapi/" } // PlaceholderAPI
}

dependencies {
    compileOnly "me.clip:placeholderapi:2.11.6"
    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"

    implementation "com.github.Revxrsal.Lamp:common:3.2.1"
    implementation "com.github.Revxrsal.Lamp:bukkit:3.2.1"
    implementation "dev.dejvokep:boosted-yaml:1.3.5"
    implementation("net.dv8tion:JDA:5.0.0-beta.24") {
        exclude module: "opus-java"
    }
    implementation "org.apache.commons:commons-lang3:3.14.0"
    implementation "org.bstats:bstats-bukkit:3.0.2"
    implementation "org.slf4j:slf4j-nop:2.0.13"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

bukkit {
    name = rootProject.name
    version = project.version
    description = project.description

    main = project.group + ".MainPlugin"
    apiVersion = "1.13"

    softDepend = ["PlaceholderAPI"]
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.shadowJar {
    minimize {
        exclude(dependency("org.slf4j:slf4j-nop:2.0.13"))
    }

    String prefix = project.group.toString() + ".libs."
    relocate "com.fasterxml", prefix + "com.fasterxml"
    relocate "com.iwebpp", prefix + "com.iwebpp"
    relocate "com.neovisionaries", prefix + "com.neovisionaries"
    relocate "dev.dejvokep", prefix + "dev.dejvokep"
    relocate "gnu.trove", prefix + "gnu.trove"
    relocate "kotlin", prefix + "kotlin"
    relocate "net.bytebuddy", prefix + "net.bytebuddy"
    relocate "net.dv8tion", prefix + "net.dv8tion"
    relocate "okhttp3", prefix + "okhttp3"
    relocate "okio", prefix + "okio"
    relocate "org.apache", prefix + "org.apache"
    relocate "org.bstats", prefix + "org.bstats"
    relocate "org.intellij", prefix + "org.intellij"
    relocate "org.jetbrains", prefix + "org.jetbrains"
    relocate "revxrsal", prefix + "revxrsal"

    archiveFileName.set("${project.name}-${project.version}.jar")
}

compileJava.dependsOn clean
build.dependsOn shadowJar